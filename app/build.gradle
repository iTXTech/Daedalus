apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

def gitCommit = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()

android {
    compileSdkVersion 31
    buildToolsVersion "31.0.0"
    defaultConfig {
        applicationId "org.itxtech.daedalus"
        minSdkVersion 21
        targetSdkVersion 31
        versionCode 21
        versionName "1.15.0"

        buildConfigField "String", "BUILD_TIME", "\"${System.currentTimeMillis()}\""
        buildConfigField "String", "GIT_COMMIT", "\"${gitCommit}\""
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions "default"
    productFlavors {
        pure {
            dimension "default"
        }
        google {
            dimension "default"
        }
    }
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = outputFileName.replace("app", "${defaultConfig.applicationId}_${defaultConfig.versionName}_${gitCommit}")
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

configurations {
    googleReleaseImplementation
}

dependencies {
    //Support
    implementation 'androidx.appcompat:appcompat:1.6.0-alpha04'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.percentlayout:percentlayout:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.google.android.material:material:1.7.0-alpha01'
    implementation 'androidx.recyclerview:recyclerview:1.3.0-alpha02'
    implementation 'androidx.preference:preference:1.2.0'
    //DNS
    implementation 'org.pcap4j:pcap4j-core:1.8.2'
    implementation 'org.pcap4j:pcap4j-packetfactory-static:1.8.2'
    implementation 'org.minidns:minidns-client:1.0.3'
    implementation 'com.google.code.gson:gson:2.9.0'
    implementation 'com.squareup.okhttp3:okhttp:4.9.3'

    //Analytics
    googleReleaseImplementation 'com.google.firebase:firebase-core:21.0.0'
    googleReleaseImplementation 'com.crashlytics.sdk.android:crashlytics:17.2.2'
}

apply plugin: 'com.google.gms.google-services'

android.applicationVariants.all { variant ->
    project.tasks.each { t ->
        if (!t.name.toLowerCase().contains("googlerelease") &&
                (t.name.contains("GoogleServices") || t.name.contains("fabric"))) {
            t.enabled = false
        }
    }
}
